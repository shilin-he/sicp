;; constructors, selectors, predicates
(define (variable? e) (symbol? e))

(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))

(define (sum? e) (and (pair? e) (eq? (car e) '+)))
(define (addend e) (cadr e))
(define (augend e) (apply make-sum (cddr e)))
(define (make-sum first . rest) 
  (cond ((and (number? first) (= first 0)) 
         (apply make-sum rest))
        ((null? rest) first)
        ; ((and (number? a2) (= a2 0)) a1)
        ; ((and (number? a1) (number? a2) (+ a1 a2)))
        (else (append (list '+ first) rest))))

(define (product? e) (and (pair? e)(eq? (car e) '*)))
(define (multiplier e) (cadr e))
(define (multiplicand e) (apply make-product (cddr e)))
(define (make-product first . rest) 
  (cond ((and (number? first) (= first 0)) 0)
        ((and (number? first) (= first 1)) 
         (apply make-product rest))
        ((null? rest) first)
        ; ((and (number? m1) (number? m2) (* m1 m2)))
        ; ((and (number? m1) (= m1 1) m2))
        ; ((and (number? m2) (= m2 1) m1))
        (else (append (list '* first) rest))))

(define (exponentiation? e) (and (pair? e) (eq? (car e) '**)))
(define (make-exponentiation base exponent)
  (cond ((= exponent 0) 1)
        ((= exponent 1) base)
        (else (list '** base exponent))))
(define (base e) (cadr e))
(define (exponent e) (caddr e))

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp) (make-sum (deriv (addend exp) var)
                              (deriv (augend exp) var)))
        ((product? exp)
         (make-sum 
           (make-product (multiplier exp) 
                         (deriv (multiplicand exp) var))
           (make-product (multiplicand exp) 
                         (deriv (multiplier exp) var))))
        ((exponentiation? exp)
         (make-product (exponent exp)
                       (make-exponentiation 
                         (base exp) 
                         (- (exponent exp) 1))
                       (deriv (base exp) var)))
        (else (error "unkown express type -- DERIV" exp))))

